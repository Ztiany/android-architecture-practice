plugins {
    id "com.android.application"
    id "kotlin-android"
    id "kotlin-kapt"
    id "dagger.hilt.android.plugin"
    id "channel"
}

kapt {
    arguments {
        arg("AROUTER_MODULE_NAME", project.name)
    }
}

/*rebuildChannel {
    baseReleaseApk file("build/outputs/apk/release/app-release-${AppConfig.versionName}.apk")
    releaseOutputDir file("apk/${AppConfig.versionName}/release/")
    //快速模式：生成渠道包时不进行校验（速度可以提升10倍以上）
    isFastMode false
}*/

android {
    compileSdkVersion AppConfig.compileSdkVersion
    buildToolsVersion AppConfig.buildToolsVersion

    signingConfigs {
        create("release") {
            v1SigningEnabled true
            v2SigningEnabled true
            keyAlias AppConfig.releaseKeyAlias
            keyPassword AppConfig.releaseKeyPassword
            storeFile rootProject.file(AppConfig.releaseKeyFileName)
            storePassword AppConfig.releaseKeyPassword
        }
    }

    defaultConfig {
        applicationId "me.ztiany.architecture.${project.name.toLowerCase()}"
        minSdkVersion AppConfig.minSdkVersion
        targetSdkVersion AppConfig.targetSdkVersion
        versionCode AppConfig.versionCode
        versionName AppConfig.versionName

        multiDexEnabled true

        vectorDrawables.useSupportLibrary = true
        resConfigs "zh", "en"//只需要中文和英文资源
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    dexOptions {
        //jumboMode = true 忽略方法数限制（这样做的缺点是apk无法在低版本的设备上安装，会出现错误：INSTALL_FAILED_DEXOPT），如果APP只兼容到19则不会有影响
        javaMaxHeapSize "4g"
    }

    buildTypes {

        //测试
        debug {
            minifyEnabled false
            shrinkResources false
            signingConfig signingConfigs.release
            manifestPlaceholders = [screenOrientation: "unspecified"]
            resValue("string", "app_name", "Android 架构模板(测试)")
            ndk {
                abiFilters 'armeabi-v7a', 'x86'
            }
        }

        //正式发布
        release {
            minifyEnabled false
            shrinkResources false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue("string", "app_name", "Android 架构模板")
            ndk {
                abiFilters 'armeabi-v7a'
            }
        }
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    sourceSets {
        main {
            if (!AppConfig.isMultiApp) {
                java.srcDirs += "src/app/java"
                res.srcDirs += "src/app/res"
                manifest.srcFile 'src/app/AndroidManifest.xml'
            }
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${project.name}-${variant.versionName}.apk"
        }
    }

    packagingOptions {
        pickFirst "lib/x86/libc++_shared.so"
        pickFirst "lib/x86_64/libc++_shared.so"
        pickFirst "lib/armeabi-v7a/libc++_shared.so"
        pickFirst "lib/arm64-v8a/libc++_shared.so"
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    testImplementation(TestLibraries.junit)
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    if (!AppConfig.isMultiApp) {
        //modules
        implementation project(":common_sdk")
        implementation project(":module_main")
        implementation project(":module_account")
        /*Hilt*/
        api AndroidLibraries.hiltDagger
        kapt AndroidLibraries.hiltDaggerApt
        /*ARouter*/
        kapt ThirdLibraries.arouterAnnotation
    } else {
        /*Hilt*/
        ap AndroidLibraries.hiltDagger
        kapt AndroidLibraries.hiltDaggerApt
        //app
        implementation AndroidLibraries.appcompat
        implementation AndroidLibraries.material
    }
}