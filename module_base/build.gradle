apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

kapt {
    arguments {
        arg("AROUTER_MODULE_NAME", project.getName())
    }
}

androidExtensions {
    experimental = true
}

boolean openDebug = isOpenDebug.toBoolean()

android {

    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode 1
        versionName "1.0"
        buildConfigField("boolean", "openDebug", "$openDebug")
        vectorDrawables.useSupportLibrary = true
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            java.srcDirs += "src/github/java"
            res.srcDirs += "src/github/res"
            if (openDebug) {
                java.srcDirs += "src/tools/java"
                res.srcDirs += "src/tools/res"
            }
        }
    }

    externalNativeBuild {
        cmake {
            //path "src/main/cpp/CMakeLists.txt"
        }
    }

}

dependencies {

    api fileTree(dir: 'libs', include: ['*.jar'])

    //测试
    testImplementation testLibraries.junit

    //基础组件库
    api project(':lib_base')//基础类库
    api project(':lib_network')//基础网络库
    api project(':lib_cache')//缓存库
    //api project(':lib_media_selector')//媒体选择器
    //api project(':lib_qrcode')//二维码
    //api project(':lib_social')//第三方登录、分享、支付
    //implementation project(':lib_push')//推送

    //Android
    api androidLibraries.multidex
    api androidLibraries.exifinterface
    api androidLibraries.flexbox

    //kotlin
    api kotlinLibraries.rxKotlin

    //Third Library(apt放前面)
    /*Dagger2*/
    kapt thirdLibraries.dagger2Apt
    kapt thirdLibraries.dagger2AndroidApt
    /*ARouter*/
    api thirdLibraries.arouter
    kapt thirdLibraries.arouterAnnotation
    /*OkHttp*/
    implementation thirdLibraries.okHttp
    implementation thirdLibraries.okHttpLogging
    /*gson*/
    api thirdLibraries.gson
    /*retrofit*/
    api thirdLibraries.retrofit
    implementation thirdLibraries.retrofitConverterGson
    implementation thirdLibraries.retrofitRxJava2CallAdapter
    /*rx binding*/
    api thirdLibraries.rxBindingCompat
    /*glideAPT*/
    kapt thirdLibraries.glideApt

    /*UI*/
    implementation(uiLibraries.androidWheelPicker) {
        exclude group: 'com.android.support'
    }
    api(uiLibraries.qmui) {
        exclude group: 'com.android.support'
    }
    api uiLibraries.circleImageView
    api uiLibraries.roundedImageView
    api uiLibraries.superTextView
    api uiLibraries.keyboardvisibilityevent

    //Debug
    if (openDebug) {
        //stetho
        implementation debugLibraries.stetho
        implementation debugLibraries.stethoOkhttp3
        //LeakCanary
        implementation debugLibraries.debugLeakCanary
        //UETool
        implementation debugLibraries.ueTool
    }

}