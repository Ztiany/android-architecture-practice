// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    def ENV_AUTO = "Auto"
    def ENV_TEST = "Test"
    def ENV_RELEASE = "Pro"

    ext {
        AppConfig = [
                minSdkVersion     : 23,
                targetSdkVersion  : 30,
                compileSdkVersion : 33,
                buildToolsVersion : "30.0.3",
                versionCode       : 1,
                versionName       : "1.0.0",
                releaseKeyAlias   : "hm",
                releaseKeyPassword: "666666",
                releaseKeyFileName: "Android.jks",
                enableProguard    : false,
                httpsTrustAll     : false,
                isOpenDebug       : false,
                showDebugTools    : false,
                isOpenLog         : true,
                HOST_ENV          : ENV_AUTO
        ]
    }
}

plugins {
    alias(libs.plugins.android.application) apply false
    alias(libs.plugins.android.library) apply false
    alias(libs.plugins.jetbrains.kotlin.android) apply false
    alias(libs.plugins.jetbrains.kotlin.parcelize) apply false
    alias(libs.plugins.jetbrains.kotlin.kapt) apply false
    alias(libs.plugins.google.hilt) apply false
    alias(libs.plugins.google.ksp) apply false
    alias(libs.plugins.vanniktech.maven.publisher) apply false
}

allprojects {
    configurations.configureEach {
        resolutionStrategy.force(
                libs.androidx.appcompat,
                libs.androidx.activity,
                libs.androidx.activity.ktx,
                libs.androidx.fragment,
                libs.androidx.fragment.ktx,
                libs.androidx.ktx,
                libs.androidx.annotations,
                libs.androidx.constraintlayout,
                libs.kotlin.stdlib,
                libs.kotlinx.coroutines
        )
    }

    tasks.withType(Javadoc).configureEach {
        // 乱码问题
        options {
            encoding "UTF-8"
            charSet 'UTF-8'
            links "http://docs.oracle.com/javase/7/docs/api"
        }
        //javadoc 编译不通过问题
        enabled = false
    }

    plugins.withId("com.vanniktech.maven.publish") {
        mavenPublish {
            sonatypeHost = "S01"
        }
    }
}