apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: 'androidx.navigation.safeargs.kotlin'

kapt {
    arguments {
        arg("AROUTER_MODULE_NAME", project.name)
    }
}

android {
    compileSdkVersion AppConfig.compileSdkVersion
    buildToolsVersion AppConfig.buildToolsVersion

    signingConfigs {
        release {
            keyAlias AppConfig.releaseKeyAlias
            keyPassword AppConfig.releaseKeyPassword
            storeFile rootProject.file(AppConfig.releaseKeyFileName)
            storePassword AppConfig.releaseKeyPassword
        }
    }

    defaultConfig {
        applicationId "me.ztiany.architecture.${project.name.toLowerCase()}"
        minSdkVersion AppConfig.minSdkVersion
        targetSdkVersion AppConfig.targetSdkVersion
        versionCode AppConfig.versionCode
        versionName AppConfig.versionName

        multiDexEnabled true

        vectorDrawables.useSupportLibrary = true
        resConfigs "zh", "en"//只需要中文和英文资源
    }

    dexOptions {
        //jumboMode = true 忽略方法数限制（这样做的缺点是apk无法在低版本的设备上安装，会出现错误：INSTALL_FAILED_DEXOPT），如果APP只兼容到19则不会有影响
        javaMaxHeapSize "4g"
    }

    buildTypes {
        //测试
        debug {
            minifyEnabled false
            shrinkResources false
            signingConfig signingConfigs.release
            manifestPlaceholders = [screenOrientation: "unspecified"]
        }

        //正式发布
        release {
            zipAlignEnabled true
            minifyEnabled false
            shrinkResources false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            ndk {
                abiFilters 'armeabi-v7a'
            }
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
        //https://stackoverflow.com/questions/68045875/for-what-purpose-is-useir-used
        useIR = true
    }

    sourceSets {
        main {
            manifest.srcFile 'src/app/AndroidManifest.xml'
            java.srcDirs += 'src/app/java/'
            res.srcDirs += 'src/app/res'
        }
    }

    //如果不想生成某个布局的绑定类，可以在根视图添加 tools:viewBindingIgnore="true" 属性。
    buildFeatures {
        viewBinding true
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion compose_version
        kotlinCompilerVersion kotlin_version
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${project.name}-${variant.versionName}.apk"
        }
    }

    packagingOptions {
        pickFirst "lib/x86/libc++_shared.so"
        pickFirst "lib/x86_64/libc++_shared.so"
        pickFirst "lib/armeabi-v7a/libc++_shared.so"
        pickFirst "lib/arm64-v8a/libc++_shared.so"
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    /* all jar*/
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    //基础类库
    implementation project(":common_sdk")

    //测试
    testImplementation TestLibraries.junit

    //navigation
    implementation AndroidLibraries.navigationFragmentKtx
    implementation AndroidLibraries.navigationUiKtx

    //compose
    implementation AndroidLibraries.composeActivity
    implementation AndroidLibraries.composeUI
    implementation AndroidLibraries.composeUIUtil
    implementation AndroidLibraries.composeUITooling
    implementation AndroidLibraries.composeUIToolingPreview
    implementation AndroidLibraries.composeUIConstraint
    implementation AndroidLibraries.composeAnimation
    implementation AndroidLibraries.composeAnimationCore
    implementation AndroidLibraries.composeAnimationGraphics
    implementation AndroidLibraries.composeFoundation
    implementation AndroidLibraries.composeFoundationLayout
    implementation AndroidLibraries.composeMaterial
    implementation AndroidLibraries.composeThemeAdapter
    implementation AndroidLibraries.composeNavigation
    implementation AndroidLibraries.composeHiltNavigation

    /*Hilt*/
    api AndroidLibraries.hiltDagger
    kapt AndroidLibraries.hiltDaggerApt

    /*ARouter*/
    kapt ThirdLibraries.arouterAnnotation
}