android {
    compileSdk AppConfig.compileSdkVersion

    signingConfigs {
        release {
            keyAlias AppConfig.releaseKeyAlias
            keyPassword AppConfig.releaseKeyPassword
            storeFile rootProject.file(AppConfig.releaseKeyFileName)
            storePassword AppConfig.releaseKeyPassword
        }
    }

    defaultConfig {
        applicationId "me.ztiany.wan.${project.name.toLowerCase()}"
        minSdk AppConfig.minSdkVersion
        targetSdk AppConfig.compileSdkVersion
        versionCode AppConfig.versionCode
        versionName AppConfig.versionName

        vectorDrawables.useSupportLibrary = true
        resConfigs "zh", "en"//只需要中文和英文资源
    }

    buildTypes {
        //测试
        debug {
            minifyEnabled false
            shrinkResources false
            signingConfig signingConfigs.release
        }

        //正式发布
        release {
            zipAlignEnabled true
            minifyEnabled false
            shrinkResources false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            ndk {
                abiFilters 'armeabi-v7a'
            }
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = "11"
        freeCompilerArgs = ["-Xcontext-receivers"]
    }

    //如果不想生成某个布局的绑定类，可以在根视图添加 tools:viewBindingIgnore="true" 属性。
    buildFeatures {
        viewBinding true
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${project.name}-${variant.versionName}.apk"
        }
    }

    packagingOptions {
        pickFirst "lib/x86/libc++_shared.so"
        pickFirst "lib/x86_64/libc++_shared.so"
        pickFirst "lib/armeabi-v7a/libc++_shared.so"
        pickFirst "lib/arm64-v8a/libc++_shared.so"
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    //基础类库
    implementation project(":common:core")

    //测试
    testImplementation libs.test.junit

    //navigation
    implementation libs.androidx.navigation.ui.ktx
    implementation libs.androidx.navigation.fragment.ktx

    /*Hilt*/
    api libs.google.hilt
    ksp libs.google.hilt.compiler
}